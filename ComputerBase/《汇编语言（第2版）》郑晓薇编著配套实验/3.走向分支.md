#第4部分 走向分支
##一、课程简介

###1.1  课程说明

本课程基于《汇编语言（第2版）》郑晓薇 编著，机械工业出版社，可以配合该教材使用。本课程由郑晓薇授权制作，提取教材中的实例以及实验内容，可以在实验楼环境中完成所有实例及实验。实验课程制作符合教材原版实例驱动教学以及实验训练贯穿始终的特点。  

###1.2  实验环境
**1.DOS 环境  **  
实验环境中安装有`dosemu`可以模拟DOS环境，并提供`DEBUG`、`MASM`、`LINK`等汇编语言开发程序。如果您对实验环境不熟悉，推荐您先学习《[新手入门之玩转实验楼](http://www.shiyanlou.com/courses/63)》。    
**2.进入汇编实验  **  
在桌面上双击dosemu图标，直接进入DOS。再做如下操作：
```
C:\〉D:           ——回车后进入D盘  
D:\〉CD DOS       ——进入DOS子目录  
D:\dos〉DIR       ——列出目录中的文件（包括masm和link）  
```  
以后的汇编实验程序都要在dos这个子目录中进行。

![img](https://dn-anything-about-doc.qbox.me/userid12501labid343time1421313062388)

##二、分支程序设计
汇编语言程序和高级语言程序一样，有顺序、分支、循环、子程序四种结构形式。计算机程序在执行过程中，可以改变程序的执行顺序，根据一定的条件进行转移，使程序完成更复杂的功能。    
汇编语言提供了无条件转移指令和条件转移指令，利用这些指令可以编制分支程序。条件转移指令是对标志位进行判断之后作转移的，也就是说在转移指令之前应该执行那些能使标志发生改变的指令。本节我们学习汇编语言分支程序的设计方法。   
###2.1  一个分支程序的例子


+ **`关注点：`条件转移指令的用法**


**示例5-1** 设计分支程序，实现下列公式计算。X、Y为字型。假设X单元中保存三个数：9，-6，34，分别作判断和计算。

![](https://dn-anything-about-doc.qbox.me/dosmu5-1.jpg)

设计思路：

（1）	在数据段中定义2个字型变量X、Y，均为带符号数；  
（2）	在X单元中依次取出三个数分别作判断，根据X的大小作分支转移；  
（3）	采用寄存器相对寻址方式（MOV AX，X[SI]）取出X的三个值；  
（4）	标号OUT1是各路分支的公共出口。  

程序框图：

![](https://dn-anything-about-doc.qbox.me/dosmu5-2.jpg)
 
实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-1.asm  用正常程序格式编写分支程序
data segment
    x dw 9,-6,34
    y dw 3 dup(?)
data ends
code segment 
	assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
    mov cx,3		；循环三次
    mov si,0				
let0:
    mov ax,x[si]	；取出X
    cmp ax,0		；X ≥0 ？
    jge let1		；是，转到let1
    mov bx,ax		；否，计算X*X
    imul bx				
    jmp out1		；跳到公共出口out1
let1:
    cmp ax,10		；X ≥10 ？
    jge let2		；是，转到let2
    sal ax,1		；否，计算2X+3
    add ax,3
    jmp out1		；跳到公共出口out1
let2:
    mov bl,6		；计算X/6
    idiv bl			；商在al，余数在ah
out1:
    mov y[si],ax	；保存Y
    add si,2
    dec cx			；CX-1
    cmp cx,0				
    jnz let0		；CX≠0转移到let0
    mov ah,4ch	；CX=0，程序结束
    int 21h
code ends
end start
```
（2）在dos子目录下保存为5-1.asm，经过汇编masm 5-1.asm，连接link 5-1.obj，生成5-1.exe。  
```
D:\dos〉masm 5-1.asm
D:\dos〉link 5-1.obj
D:\dos〉dir
D:\dos〉5-1.exe
```
（3）运行结果：  
在DOS下执行程序5-1.exe后又返回到DOS，没有显示。说明程序中没有加入显示的指令。  
```
D:\dos〉debug 5-1.exe
-U
-U
-G 0039
-D DS:0
```  
要观察运行结果，采用DEBUG执行5-1.exe。在DEBUG下，连续用反汇编U命令查看；找到断点0039，用G 0039执行；再用D命令查看结果。
![img](https://dn-anything-about-doc.qbox.me/userid12501labid358time1421331212754)  
前三个字单元分别是9，-6，34，从6号单元开始存放三个结果。若X=9，Y=0015H=21；若X=-6，Y=0024H=36；若X=34，Y的商=5，余数=4。

对上述分支程序的分析：（条件转移指令的用法可参考本书5.2节）

+ 1）程序中采用CMP比较指令对分支的条件进行判断；
+ 2）根据判断的结果用条件转移指令JGE、JNZ等进行转移。条件满足转移到标号处执行程序，条件不满足则继续执行转移指令的下一条；
+ 3）无条件转移指令JMP直接跳到标号处执行；
+ 4）转移指令所跳转的方向，既可以是正方向跳转，也可以向反方向跳转。例如程序后部的指令JNZ LET0，标号LET0在程序的前部，因此是往回跳转。可以看出，向反方向跳转构成了循环。

**`小贴士：`** 条件转移指令格式：

条件转移指令操作码    OPR  
这里的OPR代表标号，条件转移指令的作用是当条件满足时，转移到由标号指出的那条指令去执行，条件不满足则继续执行本转移指令的下一条。例如 JZ  LET1  ,表示结果为0就转移到LET1那条指令去执行。JZ
中的Z是零标志ZF，当ZF=1时，表示之前的运算结果为0。   
（1）条件转移指令包括10种指令：
+ JZ（或JE） 	   	结果为0（或相等）则转移 ，  	    测试条件：ZF=1         
+ JNZ（或JNE）  	结果不为0（或不相等）则转移，		测试条件：ZF=0   
+ JC     			结果有进位则转移 	，	 		测试条件：CF=1 
+ JNC	  			结果无进位则转移 	，		 	测试条件：CF=0
+ JS     			结果为负则转移 ，			 		测试条件：SF=1 
+ JNS	  			结果为正则转移 ，			 		测试条件：SF=0
+ JO 	  			结果溢出则转移，			 		测试条件：OF=1 
+ JNO  			结果不溢出则转移，		 		测试条件：OF=0
+ JP    			结果为偶数个1则转移，			测试条件：PF=1
+ JNP   			结果为奇数个1则转移，	 		测试条件：PF=0   

（2）无符号数比较转移指令包括4种指令：
+ JB 		低于则转移（Below），    或JNAE  不高于等于则转移		（A＜B），测试条件：CF =1且ZF=0
+ JBE		低于等于则转移（Below or Equal）， 或JNA 不高于则转移	    （A≤B），测试条件：CF=1或ZF=1 
+ JA 		高于则转移（Above），    或JNBE	 不低于等于则转移       （A＞B），测试条件：CF=0且ZF=0
+ JAE		高于等于则转移（Above or Equal），或JNB 不低于则转移	    （A≥B），测试条件：CF=0或ZF=1  

（3）带符号数比较转移指令包括4种指令：（由于带符号数的最高位为符号位，因此带符号数的数值与无符号数不一样，要用另外的比较转移指令）  
+ JL	 小于则转移（Less），    或JNGE  不大于等于则转移	    （A＜B） ，  测试条件：SF≠OF且ZF=0 
+ JLE		小于等于则转移（Less or Equal ）， 或JNG  不大于则转移    （A≤B），    测试条件： SF≠OF或ZF=1  
+ JG		大于则转移（Greater），   或JNLE	 不小于等于则转移	    （A＞B），测试条件：SF=OF且ZF=0 
+ JGE		大于等于则转移（Greater or Equal），或JNL 不小于则转移		（A≥B），测试条件：SF=OF或ZF=1



**示例5-2** 设计分支程序。计算 Y=5X-18，如果结果为负，求绝对值。并显示十进制结果。

设计思路：

（1）	用数据段保存X、Y。为简便，X定义为字节，Y定义为字；  
（2）	用符号位SF判断运算结果的正负，为负数则求补（绝对值），如果是正数，直接保存结果；  
（3）	采用将AX中的结果除以10、取得余数的方法获得结果的十进制数；  
（4）	将余数变为ASCII码，用DOS中断调用的2号功能显示出来；  
（5）	用9号功能显示提示信息。

程序框图：

![](https://dn-anything-about-doc.qbox.me/dosmu5-4.jpg)
 
实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-2.asm  计算Y=5X-18，用正常程序格式
data segment
    x db  -6
    y dw ?
    cc  db 0ah,0dh,'Y=$'
data ends
code segment 
    assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
    mov al,5		；5X
    imul x
    sub ax,18		；-18
    jns let0		；结果不为负则转移
    neg ax		；结果为负，求绝对值
let0:
    mov y,ax		；保存结果
    ；将ax中的二进制数变为十进制数，并显示
    mov cx,0
    mov bx,10				
let1:						
    mov dx,0
    inc cx			；统计余数个数
    idiv bx		；AX/10，商在AX，余数在DX
    push dx		；保存余数
    cmp ax,0		；商为0，则退出循环
    jnz let1
    mov dx,offset cc ；9号功能显示提示
    mov ah,9
    int 21h
let2:			；循环执行cx次，显示十进制结果	
    pop ax		；将余数弹入ax
    add ax,0030h	；调整为ASCII码
    mov dl,al		；2号功能，显示一个字符
    mov ah,2
    int 21h
    dec cx
    cmp cx,0
    jnz let2
    mov ah,4ch
    int 21h
code ends
end start
```
（2）在dos子目录下保存为5-2.asm，经过汇编masm 5-2.asm，连接link 5-2.obj，生成5-2.exe。  
```
D:\dos〉masm 5-2.asm
D:\dos〉link 5-2.obj
D:\dos〉5-2.exe
```
（3）运行结果： 

![](https://dn-anything-about-doc.qbox.me/dosmu5-5.jpg)   

**`思考：`** 如果从键盘输入X，并且允许输入负数-6，程序如何修改？  
**`提示：`** 增加键盘输入指令，先判断从键盘输入的是否为“-”负号；如果是，再执行一次键盘输入，把输入的数去掉ASCII码（AND AL,0FH）、求绝对值（NEG AL），并将AL保存到x单元。

**练习：** 编写程序，X、Y都是大于0的数。若X≥Y，Z＝X－Y；否则，Z=Y／X，以十进制显示结果。除法运算仅显示商。

**`思考：`** 如果除法运算既要显示商又要显示余数，程序如何修改？ 

###2.2 	数字和字母的判断

从前面章节的学习中，我们知道，无论是键盘输入的数字或字母，还是在存储区中定义的字符，都是以ASCII码形式表示的，在程序中经常要对它们进行判断。对数字或字母的判断有多种方法。本节我们采用位操作的方法来区分数字和大小写字母，and、or、not（与或非）逻辑指令和test测试指令都是按位操作的。

+ **`关注点：`与或非逻辑指令的用法**

**示例5-3**  从键盘输入一串字符，如果是数字存入NUMB单元，如果是字母，将大写字母存入CAPI单元，小写字母存入LETT单元，分别统计个数，输入回车时退出。

设计思路：

（1）  用TEST测试指令来区分数字和字母：

![img](https://dn-anything-about-doc.qbox.me/userid12501labid358time1421499987985)

数字和字母的第6位不同。因此，区别数字和字母用TEST AL,40H；第6位为0是数字，第6位为1 则为字母。
大小写字母为第5位不同。区别大小写字母用TEST AL,20H；第5位为0是大写字母，第5位为1则为小写字母。  
（2）	用CMP指令排除其它字符；  
（3）	数字、大写字母、小写字母的个数分别放在DI、SI、BX中。

实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-3.asm  分支程序。区分和统计键入的数字、大写字母、小写字母
data segment
    numb db 10 dup(?)
    capi db 10 dup(?)
    lett db 10 dup(?)
data ends
code segment
    assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
let0: mov ah,1		;键盘输入
    int 21h
    cmp al,0dh		;回车?
    jz exit				;是，转EXIT
    test al,40h			;区分数字和字母
    jz let1				;是数字，转LET1
    test al,20h			;区分大小写字母
    jz let2				;是大写，转LET2
    cmp al,7ah          ;排除不是小写字母
    ja exit
    mov lett[bx],al		
    inc bx				;小写个数加1 
    jmp let0
let2: cmp al,5ah		;排除大小写之间的字符
    ja exit
    mov capi[si],al
    inc si			    ;大写个数加1		
    jmp let0
let1: cmp al,'0'		;排除其它字符
    jb exit
    cmp al,'9'
    ja exit
    mov numb[di],al
    inc di				;数字个数加1
    jmp let0
exit: mov ah,4ch
    int 21h
code ends
    end start
```
（2）在dos子目录下保存为5-3.asm，经过汇编masm 5-3.asm，连接link 5-3.obj，生成5-3.exe。  
```
D:\dos〉masm 5-3.asm
D:\dos〉link 5-3.obj
D:\dos〉5-3.exe
```
（3）运行结果：   
由于本程序没有输出功能，若要查看运行结果，需要在DEBUG下运行。
``` 
D:\dos〉debug 5-3.exe
-u
-u
-g 3e
```
![img](https://dn-anything-about-doc.qbox.me/userid12501labid358time1421333560095)

多次执行u命令，找到程序后部的MOV AH,4CH指令，设置本程序的断点在003e，即MOV AH,4CH处。执行G 3e之后，从键盘输入若干字符“23sdfAZ”（如上图所示），回车后显示出各个寄存器的值。其中，数字个数DI=2，大写字母个数SI=2，小写字母个数BX=3。在数据段内存中，分别保存了键入的数字和大小写字母。  
**`小贴士：`** TEST  测试指令的用法。TEST指令将两个操作数做按位相与操作，结果不回送，但是改变了标志位。  
例如：假设AL=97H，要测试AL的第3位是否为0；如果为0，转移到LET1执行。指令为TEST  AL，08H。把数值转换成二进制：97H=10010111，08H=00001000。可看到，08H只有第3位为1，其余位都是0，任何数和08H相与的结果只保留了第3位的值，其余为全部清0。如果第3位上为0，那么整个结果就为0，否则结果不为0，零标志ZF会相应地做出改变。这样，根据结果是否为0就可以进行分支转移了。 

![img](https://dn-anything-about-doc.qbox.me/userid12501labid358time1421935409696)
  
**练习：**
+ （1）画出5-3.ASM的流程图。
+ （2）在5-3.ASM中增加显示功能，分别显示提示信息和统计个数值。


**示例5-4**  从键盘输入英文单词，将其中的小写字母变为大写。

设计思路：

（1）	用AND指令将小写字母的ASCII码的第5位变为0即为大写字母；  
（2）	用DOS的9号功能显示提示信息；  
（3）	用DOS的10号功能输入英文字母。

实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-4.asm  输入英文单词,将小写字母转换为大写。
data segment
    mess1 db 0ah,0dh,'input:$'
    mess2 db 0ah,0dh,'output:$'
    buff db 10,?,10 dup(?)
data ends
code segment
    assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
prog1:
    mov dx,offset mess1				;显示提示1
    mov ah,9
    int 21h
    mov dx,offset buff				;输入字串
    mov ah,10
    int 21h
    mov cl,buff+1					;实际输入的字母个数
    mov bx,2							;第一个字母的地址
    mov dx,offset mess2				;显示提示2
    mov ah,9
    int 21h
let1:
    and buff[bx],0dfh				;小写字母变为大写
    mov dl,buff[bx]					;循环显示每个字母
    mov ah,2		
    int 21h
    inc bx
    dec cl
    jnz let1			
    mov ah,4ch
    int 21h
code ends
    end start
```

（2）在dos子目录下保存为5-4.asm，经过汇编masm 5-4.asm，连接link 5-4.obj，生成5-4.exe。  
```
D:\dos〉masm 5-4.asm
D:\dos〉link 5-4.obj
D:\dos〉5-4.exe
```
（3）运行结果：   
![](https://dn-anything-about-doc.qbox.me/dosmu5-7.jpg)      
**`分析思考:`**  
+ 本例采用“逻辑与”AND指令将某位屏蔽为0。  
 and buff[bx],0dfh   该指令中源操作数为十六进制dfh，写成二进制为11011111，可看出除了第5位为0，其余都是1 。因此，不论目的操作数是何值，只要是和DFH相与，结果的第5位清0，其余位保持不变。这样就达到了将小写字母变为大写字母的目的。   
“与”AND、“或”OR、“非”NOT等逻辑指令的用法见本书5.3节。

**示例5-5**  计算0～9的立方值并显示。

设计思路：

（1）	从键盘输入0～9；可多次输入，按ESC键退出；  
（2）	将输入的数字去掉ASCII码；  
（3）	用连乘计算立方值，注意百位的判断；  
（4）	用除以10取余得到百位、十位、个位数，并用x的三个单元分别存放；  
（5）	显示十进制结果。

实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-5.asm  计算0-9立方值。ESC退出
data segment
    mess1 db 0ah,0dh,'input:$'
    mess2 db 0ah,0dh,'output:$'
    x     db ?,?,?
data ends
code segment
    assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
let0:mov dx,offset mess1				;显示提示
    mov ah,9
    int 21h
    mov ah,1							;输入0-9
    int 21h
    cmp al,27							;按ESC退出
    jz out1
    cmp al,'0'							;输入限制
    jb let0
    cmp al,'9'
    ja let0
    and al,0fh							;去掉ASCII码
    mov ah,0
    mov bl,al							;求立方
    mul bl
    mul bl								;立方值在ax
    mov bl,10							;立方值变为十进制
    div bl
    add ah,30h							;个位加上ASCII码
    mov x,ah							;x 保存个位
    cmp al,10							;高位≥10?
    jb let2								;小于转移
    mov ah,0							;≥10继续除以10
    div bl
    add ah,30h							;十位加上ASCII码
    mov x+1,ah							;x+1 十位
    add al,30h						
    mov x+2,al							;x+2 百位
    jmp let3
let2:add al,30h							;十位数
    mov x+1,al
let3:mov ax,0
    mov dx,offset mess2
    mov ah,9
    int 21h
    mov dl,x+2							;显示立方值
    mov ah,2
    int 21h
    mov dl,x+1
    int 21h
    mov dl,x
    int 21h
    mov word ptr x,0					;将x单元清0
    jmp let0							;返回,再输入数字
out1:mov ah,4ch							;退出
    int 21h
code ends
    end start
```

（2）在dos子目录下保存为5-5.asm，经过汇编masm 5-5.asm，连接link 5-5.obj，生成5-5.exe。 
```
D:\dos〉masm 5-5.asm
D:\dos〉link 5-5.obj
D:\dos〉5-5.exe
``` 
（3）运行结果： 
 
![](https://dn-anything-about-doc.qbox.me/dosmu5-8.jpg)

   
**`思考：`**求立方值还可以用查立方表的方法实现。结合查表操作，修改程序。

**示例5-6**  从键盘输入两个两位的十进制数，做加法运算，并显示结果。

从键盘输入数字0～9的ASCII码为30H～39H，要想用它们做十进制运算可以将其转换为BCD码。数字的ASCII码与压缩BCD码之间的转换有多种方式，此处采用位运算实现。

+ **`关注点：`BCD码，移位操作和十进制运算调整指令**

设计思路：

（1）	键盘输入一个两位数之后回车，再输入另外一个两位数。将4个数去掉30H保存到X单元。比如输入12和34，相加结果应为46；    
（2）	用移位操作将4个数据两两合并为相应的BCD码；  
（3）	相加后用DAA十进制调整指令调整，如果有百位的进位，用变量Z记住；  
（4）	用移位以及加3030H操作将BCD码再变为ASCII码，显示百位、十位、个位数值。

实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-6.asm  输入两个2位十进制数，相加并显示结果。
data segment
    x db 4 dup(?)
    z db ?  
data ends
code segment
assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
    mov cx,2				;允许输入两个数据
    mov si,0
let0:
    mov ah,1				;键盘输入12↓34↓
    int 21h
    cmp al,0dh				;回车?
    jz let1					;是，转let1输入第2个数
    and al,0fh				;去掉30h
    mov x[si],al			;保存到x
    inc si
    jmp let0
let1:
    mov ah,2				；换行
    mov dl,0ah
    int 21h
    dec cx
    jnz let0				
    ;将保存在x单元中的01、02、03、04合并为BCD码
    mov ax,0
    mov al,x				;第1个数变为BCD码12H
    mov cl,4
    shl al,cl				;01左移4位后变为10
    add al,x+1
    mov bl,x+2				;第2个数变为BCD码34H
    shl bl,cl				;03左移4位后变为30
    add bl,x+3
    add al,bl				;BCD码相加后ax=0046
    daa						;对AX作十进制调整
    ;显示十进制结果
    jnc let2				;没有百位则转let2
    mov z,'1'				;标记有百位进位
let2:
    mov cl,4
    shl ax,cl				;左移4位，ax=0460
    rol al,cl				;al左移4位后ax=0406
    add ax,3030h			;ax=3436——将BCD码变为ASCII码
    mov bx,ax
    mov dl,0ah				;换行显示
    mov ah,2
    int 21h
    cmp z,'1'				;有百位?
    jnz out1
    mov dl,z				;显示百位数1
    int 21h
out1:
    mov dl,bh				;显示十位
    int 21h
    mov dl,bl				;显示个位	
    int 21h
    mov ah,4ch
    int 21h
code ends
    end start
```

（2）在dos子目录下保存为5-6.asm，经过汇编masm 5-6.asm，连接link 5-6.obj，生成5-6.exe。  
```
D:\dos〉masm 5-6.asm
D:\dos〉link 5-6.obj
D:\dos〉5-6.exe
```
（3）运行结果： 

![](https://dn-anything-about-doc.qbox.me/dosmu5-9.jpg)
 
第1次执行 12+34=46，第2 次执行78+89=167。

**`注意：`** 若只输入个位数，必须输入01、02 等，保持两位数的输入。  
**`小贴士：`** **移位操作**

![图片描述信息](https://dn-anything-about-doc.qbox.me/userid12501labid358time1424922886476)
![图片描述信息](https://dn-anything-about-doc.qbox.me/userid12501labid358time1424922910070)

 有关移位指令的其他用法参考本书5.3.5节。
 
**`小贴士：`** **十进制调整指令** 

BCD码是用二进制编码来表示十进制数，我们希望在计算机中按十进制运算规则进行运算，但计算机实际上进行的是二进制运算。例如十进制运算5+7=12，用BCD码表示为0101+0111。按照二进制相加，结果等于1100，而这个结果不是BCD码；那么再把它加6，结果就是00010010，即BCD码表示的12。  
通过观察二进制数运算结果和对应的十进制运算结果的差别可知，只要是结果大于9，就应该对计算结果做修正调整，这样获得的数值就符合逢十进一的十进制运算规则。8086指令系统中提供了相关的十进制调整指令。有关十进制调整指令的用法参考本书4.3.7节。

**`思考：`** 如果显示提示信息，程序如何修改？ 


##三、 走向分支
**本节实验取自教材中第五章的《实例五 走向分支》。**

分支判断是计算机具有智能的表现，而这种智能是受控于人的意志的。也就是说，需要编写程序人员的智慧和判断，设计出计算机能理解的指令程序供机器运行。

###3.1 分支的选择

在分支程序设计中，要通过条件判断做转移。而条件转移指令有四种类型，根据标志的转移、无符号数比较转移、带符号数比较转移和CX为0转移。具体用哪种转移指令更好呢？ 这需要根据题目内容来选择。

**示例5-7**  十进制与十六进制转换。将键盘输入的一个两位十进制数以十六进制形式显示在屏幕上。可多次输入直到按下ESC键。

设计思路：  
（1）	用DOS的1号功能输入一个两位数，以回车结束；  
（2）	将输入的数字减去30H保存在X单元，第1个数字扩大10倍再与第2个数相加，变为十进制数；  
（3）	用9号功能显示提示信息；    
（4）	将十进制数除以16，形成十六进制数；  
（5）	再将十六进制数转换为ASCII码，用2号功能显示。

实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：  
```
;5-7.asm  可多次输入一个两位十进制数并以十六进制显示出来，按ESC键退出。
data segment
    x db 2 dup(?)
    mess1 db 0dh,0ah,'decimal=$'
    mess2 db 0dh,0ah,'HEX=$'
data ends
code segment
    assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
let0:
    mov x,0
    mov x+1,0
    mov si,0
    mov dx,offset mess1	;显示提示1
    mov ah,9
    int 21h
let1:
    mov ah,1				;键盘输入十进制数
    int 21h
    cmp al,27				;是ESC键?
    jz out1
    cmp al,0dh			;回车?
    jz let2							;是，转let2
    and ax,000fh					;去掉ASCII码
    mov x[si],al					;保存到x
    inc si							;统计输入的位数
    jmp let1
let2:
    mov dx,offset mess2				;显示提示2
    mov ah,9
    int 21h
    cmp si,1						;判断输入的位数
    ja let3							;输入了两位数转let3
    mov bl,x
    mov cl,1
    jmp let5						;只输入1位数则直接去显示
let3:
    mov al,x
    mov cl,10
    mul cl							;形成两位十进制数
    add al,x+1
    mov ah,0
    mov bl,16						;除以16，转换为十六进制
    div bl
    mov bx,ax						;AH为余数即低位，AL为商即高位
    ;分别显示十六进制高位、低位
    mov cl,2
let4:
    cmp bl,10						;判断十六进制数码
    jl let5
    add bl,7						; ≥10 则加7（是字母A～F） 
let5:								
    add bl,30h						;加上ASCII码
    mov dl,bl
    mov ah,2						;显示
    int 21h
    mov bl,bh						;再去显示低位
    dec cl
    jnz let4
    jmp let0						;返回let0继续输入
out1:
    mov ah,4ch
    int 21h
code ends
    end start
```

（2）在dos子目录下保存为5-7.asm，经过汇编masm 5-7.asm，连接link 5-7.obj，生成5-7.exe。
```
D:\dos〉masm 5-7.asm
D:\dos〉link 5-7.obj
D:\dos〉5-7.exe
```  
（3）运行结果： 

![](https://dn-anything-about-doc.qbox.me/dosmu5-10.jpg)

 
**`分析思考：`**

+ （1）键盘输入和结果显示是一个用户程序的常用功能。我们希望键盘输入的是十进制数，进行运算后能显示出十进制或者十六进制数。而键盘输入和屏幕显示涉及到对外设硬件的访问，由于汇编语言是直接控制机器的符号语言，没有提供类似的功能函数或语句，需要自己设计相关的程序段实现这个功能。
+ （2）这段程序中用到的转移指令有一些是往回跳转的，无论是无条件转移JMP还是条件转移指令JNZ等都有这种转移方向。这种分支走向已经把程序的流程构成了一个环，就是循环结构。
+ （3）画出5-7.asm程序流程图。
+ （4）这个程序只能输入一个两位的十进制数，如果能输入多位十进制数转换成十六进制数，那么就是一个小的工具软件了。程序的修改可参考7.1.2节示例7-1。

###3.2 菜单程序设计

当我们设计菜单程序时，如果有3项功能，希望分别按下1、2、3键就转移到3个不同的程序段执行。菜单程序的设计可以利用分支程序实现。

**示例5-8**  设计菜单程序。实现三个功能：  
 1、小写字母转换为大写  
 2、计算立方值  
 3、退出  

设计思路：  
（1）	在数据段中定义菜单。每行换行显示；  
（2）	用9号功能显示菜单；  
（3）	根据输入选择执行某段程序。

实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-8.asm  菜单程序设计1、输入字串，将小写转换为大写2、计算0-9立方值3、退出。
data segment
    mess0 db 0ah,0dh,'1. input string'
          db 0ah,0dh,'2. calculate cube'
          db 0ah,0dh,'3. exit'
          db 0ah,0dh,'select:$'
    mess1 db 0ah,0dh,'input:$'
    mess2 db 0ah,0dh,'output:$'
    buff  db 10,?,10 dup(?)
    x     db ?,?,?
data ends
code segment
    assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
let0:
    mov dx,offset mess0				;显示菜单
    mov ah,9
    int 21h
    mov ah,1							;输入选择
    int 21h 
    cmp al,'1'
    jz prog1
    cmp al,'2'
    jz prog2
    jmp prog3						;按其它键均退出
    prog1:							;菜单1
    ;5-4.asm  程序部分
    ;输入英文单词,将小写字母转换为大写
    ;……
    jmp let0							;返回主菜单
    prog2:							;菜单2
    ;5-5.asm  程序部分
    ;计算0-9立方值
    ;……
    jmp let0							;返回主菜单
    prog3:							;菜单3.退出
    mov ah,4ch
    int 21h
    code ends
    end start
```
（2）在dos子目录下保存为5-8.asm，经过汇编masm 5-8.asm，连接link 5-8.obj，生成5-8.exe。 
```
D:\dos〉masm 5-8.asm
D:\dos〉link 5-8.obj
D:\dos〉5-8.exe
``` 
（3）运行结果： 

![](https://dn-anything-about-doc.qbox.me/dosmu5-11.jpg)

**`分析思考：`**      
+ （1）这个菜单中的前两个功能程序5-4.asm和5-5.asm在前几节中我们已经编写过，加入到菜单程序中时要注意只加入该程序的主要部分而不是全部。还要注意各段程序的标号不能冲突。
+ （2）菜单选择部分采用比较指令CMP AL,’1’和条件转移指令JZ PROG1实现分支，这种设计思想对于菜单项较少的情况比较简便；当菜单项增多时，比较指令和条件转移指令就要多次使用，程序冗长。在这种情况下，可以采用教材5.5.3节中介绍的分支表的方法。	

### 3.4 实验示例
 + **`关注点：`如何显示十六进制数   **

**示例5-10**  用查表的方法将内存单元中的字用十六进制显示出来。
   
设计思路：  
（1）	设内存单元X1中共有4个字。这些数据以十进制形式书写，在汇编时，系统自动变为二进制保存，我们可以将其看成是十六进制。比如49保存后是0031H，298是012AH；  
（2）	建立一个十六进制数码表HEX。用查表方式可取得相对应的十六进制数的字符；  
（3）	1位十六进制数是由4位二进制组成，用循环左移4位的方式将要显示的数位移到最低4位处，将其余的高12位都清零；  
 （4）	采用HEX[DI]寄存器相对寻址方式查表，以HEX表地址作为相对量，用DI寄存器存放十六进制某一位数，对应取出字符；改变DI的值可取出不同的数位值；  
（5）	用DOS的2号功能显示该数位值。

实验步骤：

（1）双击桌面上的记事本`gedit`，录入下列程序：

```
;5-10.asm  用查表方法将内存单元中的字以十六进制显示出来。
data segment
    x1 dw 49,298,23456,65530
    count db 4
    hex db '0123456789ABCDEF'     ；十六进制数码表
    mess db 0dh,0ah,'HEX=$'
data ends
code segment
    assume cs:code,ds:data
start:
    mov ax,data
    mov ds,ax
    mov si,0
let0:
    mov dx,offset mess			;显示提示
    mov ah,9
    int 21h
    mov bx,x1[si]				;取出x1，如49=0031h
    mov ch,4
    mov cl,4
let1:							;每次循环左移4位
    rol bx,cl					;0031→0310→3100→1003→0031
    mov ax,bx					
    and ax,000fh				;保留最低4位
    mov di,ax					;放入DI
    mov dl,hex[di]				;查表显示某位
    mov ah,2					;显示
    int 21h
    dec ch
    jnz let1					;返回let1显示十六进制数的下一位
    add si,2
    dec count					；X1的4个字都显示了？
    jnz let0					;返回let0，显示下一字
out1:
    mov ah,4ch
    int 21h
code ends
    end start
```

（2）在dos子目录下保存为5-10.asm，经过汇编masm 5-10.asm，连接link 5-10.obj，生成5-10.exe。 
```
D:\dos〉masm 5-10.asm
D:\dos〉link 5-10.obj
D:\dos〉5-10.exe
``` 
（3）运行结果： 

![](https://dn-anything-about-doc.qbox.me/dosmu5-12.jpg)

   
**`分析思考：`**  
+  （1）这个程序也是要显示十六进制数，但是不采用将数值除以16的方法，而是用查字符表的方式。可以对多位十进制数（不超过65535）转换，方法简便。
+ （2）本程序结合了移位指令、逻辑指令和转移指令实现分支。
+ （3）由于是字单元，因此最多显示4位十六进制数。如果要显示更多位，如何编程？

###3.5 实验任务

####1.实验目的

通过分析和运行示例程序，对分支程序设计有更深一步的了解。掌握分支程序设计方法，设计出具有自己风格的分支程序。

####2.实验内容
（1）菜单程序设计  
参考示例5-8设计菜单程序，包含示例5-2、示例5-3及退出三个菜单项。将示例5-2改为从键盘输入X；示例5-3增加显示功能，显示出统计个数。  
（2）分支程序设计  
参考示例5-10和示例5-7，完成下列实验内容：  

+ 1）修改示例程序，显示出二进制数。显示结果供参考：

![](https://dn-anything-about-doc.qbox.me/dosmu5-13.jpg)

+  2）5-7.ASM是一个十进制与十六进制转换的小工具，改写为输入十进制数0～255，显示出相应的十六进制数。

#### 3.实验要求
+ 1）画出程序框图；
+ 2）实验内容用截图形式记录实验结果；
+ 3）写出实验结果分析。

#### 4.实验拓展
+ 1）参考第9章示例9-3，在屏幕上清屏、开窗口，将菜单程序带颜色地显示在窗口中。提示：可利用宏库9-4.MAC中的功能。
+ 2）编写一个将AX寄存器中值依次循环左移1位、并依次显示出该十六进制数的程序。







